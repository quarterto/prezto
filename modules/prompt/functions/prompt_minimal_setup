#
# A monochrome theme that displays basic information.
#
# Authors:
#   Brian Tse <briankftse@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/zLZNK.png
#

PROMPT_CHAR="â¯"
VCS_ADDED="âœ“"
VCS_CHANGES="âœ¶"
VCS_UNTRACKED="âœ—"
EXIT_CODE="âŽ"

function +vi-git_status() {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]="%F{red}${VCS_UNTRACKED}%f"
  fi
}

# turns seconds into human readable time
# 165392 => 1d 21h 56m 32s
prompt_pure_human_time() {
  local tmp=$1
  local days=$(( tmp / 60 / 60 / 24 ))
  local hours=$(( tmp / 60 / 60 % 24 ))
  local minutes=$(( tmp / 60 % 60 ))
  local seconds=$(( tmp % 60 ))
  echo -n "ðŸ•™  "
  (( $days > 0 )) && echo -n "${days}d "
  (( $hours > 0 )) && echo -n "${hours}h "
  (( $minutes > 0 )) && echo -n "${minutes}m "
  echo "${seconds}s"
}

# displays the exec time of the last command if set threshold was exceeded
prompt_pure_cmd_exec_time() {
  local stop=$(date +%s)
  local start=${cmd_timestamp:-$stop}
  integer elapsed=$stop-$start
  (($elapsed > ${PURE_CMD_MAX_EXEC_TIME:=5})) && prompt_pure_human_time $elapsed
}

prompt_minimal_preexec() {
  cmd_timestamp=$(date +%s)
}
function prompt_minimal_precmd {
  prompt_minimal_is_ssh
  vcs_info
  time_taken="%F{yellow}`prompt_pure_cmd_exec_time`%f"
  unset cmd_timestamp
}

function prompt_minimal_is_ssh {
	SESSION_TYPE=""
	if [ -n "$SSH_CLIENT" ] || [ -n "$SSH_TTY" ]; then
	  SESSION_TYPE="%F{yellow}%m%f "
	# many other tests omitted
	else
	  case $(ps -o comm= -p $PPID) in
	    sshd|*/sshd) SESSION_TYPE="%F{yellow}%m%f ";;
	  esac
	fi
}

function prompt_minimal_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_minimal_precmd
  add-zsh-hook preexec prompt_minimal_preexec

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' stagedstr "%F{green}${VCS_ADDED}%f"
  zstyle ':vcs_info:*' unstagedstr "%F{yellow}${VCS_CHANGES}%f"
  zstyle ':vcs_info:*' formats '%F{magenta}%b %c%u%f'
  zstyle ':vcs_info:*' actionformats "%b %c%u|%F{cyan}%a%f"
  zstyle ':vcs_info:(sv[nk]|bzr):*' branchformat '%b|%F{cyan}%r%f'
  zstyle ':vcs_info:git*+set-message:*' hooks git_status

  # Define prompts.
  PROMPT='${SESSION_TYPE}%F{green}%2~%f %(?.%F{cyan}.%F{red})${PROMPT_CHAR}%f '
  RPROMPT='${time_taken} %(?..%F{red}%?${EXIT_CODE} %f) ${vcs_info_msg_0_}'
}

prompt_minimal_setup "$@"
